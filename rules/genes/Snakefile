# Map mRNA sequences to BACs.

rule duplicated_genes:
    input: genes="mRNA.named.bed", wssd="wins/wssdGE10K_nogap.tab"
    output: "duplicated_genes_per_accession.tab"
    params: sge_opts=""
    shell: "bedtools intersect -a {input.genes} -b {input.wssd} -wa | cut -f 1,4 | sort -k 1,1 -k 2,2 | uniq > {output}"

rule genes_for_all_accessions:
    input: config["clone_ids"], "genes_by_accession.tab"
    output: "genes_for_all_accessions.tab"
    params: sge_opts=""
    shell: """join -j 1 -a 1 -e 0 -o 1.1 2.2 -t"	" {input} | cut -f 2 | awk 'OFS="\\t" {{ if (NR == 1) {{ print "total_genes" }} print }}' > {output}"""

rule genes_by_accession:
    input: "mRNA.named.bed"
    output: "genes_by_accession.tab"
    params: sge_opts=""
    shell: "cut -f 1,4 {input} | sort -k 1,1 -k 2,2 | uniq | groupBy -i stdin -g 1 -c 2 -o count > {output}"

rule get_name_for_mRNA_accession:
    input: "mRNA_by_accession.bed", "refseq_name_by_accession.tab"
    output: "mRNA.named.bed"
    params: sge_opts=""
    shell: """join -1 4 -2 1 {input} | awk 'OFS="\\t" {{ print $2,$3,$4,$13,$5,$6,$7,$8,$9,$10,$11,$12 }}' | sort -k 1,1 -k 2,2n | uniq > {output}"""

rule sort_mRNA_bed_by_accession:
    input: "mRNA.bed"
    output: "mRNA_by_accession.bed"
    params: sge_opts=""
    shell: "sort -k 4,4 {input} > {output}"

rule get_refseq_name_by_accession:
    input: "refLink.txt.gz"
    output: "refseq_name_by_accession.tab"
    params: sge_opts=""
    shell: """zcat {input} | awk 'BEGIN {{ OFS="\\t"; FS="\\t" }} {{ print $3,$1 }}' | sort -k 1,1 > {output}"""

rule get_refseq_lookup_table:
    output: "refLink.txt.gz"
    params: sge_opts=""
    shell: "wget %s -O {output}" % config["mrna_link_url"]

rule convert_BAM_to_BED:
    input: "mRNA.sorted.bam"
    output: "mRNA.bed"
    params: sge_opts="-l mfree=1G"
    shell: "bedtools bamtobed -i {input} -bed12 > {output}"

rule map_mRNA:
    input: index="bacs_index", mrna="mRNA.fasta"
    output: "mRNA.sorted.bam"
    params: sge_opts="-l mfree=4G -pe serial 4"
    shell: "gmap -D `pwd` -d {input.index} -B 4 --min-identity=0.99 -t 4 --nofails --npaths=1 -A -f samse {input.mrna} 2> /dev/null | samtools view -Sbu -q 30 - | samtools sort -o {output} -O bam -T tmp_sort -"

rule build_GMAP_index:
    input: "clones.fasta"
    output: "bacs_index"
    params: sge_opts="-l mfree=5G"
    log: "gmap_build.log"
    shell: "gmap_build -D `pwd` -d {output} -k 15 -b 12 {input} &> {log}"

rule extract_mRNA:
    input: "refMrna.fa.gz"
    output: "mRNA.fasta"
    params: sge_opts=""
    shell: "gunzip -c {input} > {output}"

rule get_mRNA:
    output: "refMrna.fa.gz"
    params: sge_opts=""
    shell: "wget %s -O {output}" % config["mrna_url"]
